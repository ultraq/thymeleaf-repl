/* 
 * Copyright 2021, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

import java.time.ZoneId
import java.time.ZonedDateTime

plugins {
	id 'groovy'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.springframework.boot' version '3.1.5'
	id 'idea'
}
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.3.1/gradle-support.gradle'

description = 'A website for being able to try out snippets of Thymeleaf code'
version = ZonedDateTime.now(ZoneId.of("Pacific/Auckland")).format('yyyy.MM.dd-\'build\'HHmmss')

java {
	sourceCompatibility = JavaVersion.VERSION_17
}

def groovyVersion = '4.0.15'
def thymeleafVersion = '3.1.2.RELEASE'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'ch.qos.logback:logback-classic:1.4.11'
	implementation 'javax.inject:javax.inject:1'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-spring-environment-dialect:1.2.0'
	implementation "org.apache.groovy:groovy:${groovyVersion}"
	implementation "org.apache.groovy:groovy-json:${groovyVersion}"
	implementation 'org.slf4j:slf4j-api:2.0.9'
	implementation "org.thymeleaf:thymeleaf:${thymeleafVersion}"
	implementation "org.thymeleaf:thymeleaf-spring5:${thymeleafVersion}"

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

processResources {
	filesMatching('**/*.properties') {
		filter(ReplaceTokens, tokens: [
			'thymeleafVersion': thymeleafVersion,
			'version': version
		])
	}
}
